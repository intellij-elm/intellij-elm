{
    generate = [ psi = "no" ]
    psiClassPrefix="Elm"
    parserClass="org.elm.lang.core.parser.ElmParser"
    elementTypeHolderClass="org.elm.lang.core.psi.ElmTypes"
    parserUtilClass="org.elm.lang.core.parser.manual.ElmManualParseRules"
    elementTypeClass="org.elm.lang.core.psi.ElmElementType"
    tokenTypeClass="org.elm.lang.core.psi.ElmTokenType"

    elementTypeFactory("ModuleDeclaration")="org.elm.lang.core.stubs.StubImplementationsKt.factory"
    elementTypeFactory("TypeDeclaration")="org.elm.lang.core.stubs.StubImplementationsKt.factory"
    elementTypeFactory("TypeAliasDeclaration")="org.elm.lang.core.stubs.StubImplementationsKt.factory"
    elementTypeFactory("UnionMember")="org.elm.lang.core.stubs.StubImplementationsKt.factory"
    elementTypeFactory("FunctionDeclarationLeft")="org.elm.lang.core.stubs.StubImplementationsKt.factory"
    elementTypeFactory("InfixDeclaration")="org.elm.lang.core.stubs.StubImplementationsKt.factory"
    elementTypeFactory("ExposingList")="org.elm.lang.core.stubs.StubImplementationsKt.factory"
    elementTypeFactory("ExposedOperator")="org.elm.lang.core.stubs.StubImplementationsKt.factory"
    elementTypeFactory("ExposedValue")="org.elm.lang.core.stubs.StubImplementationsKt.factory"
    elementTypeFactory("ExposedType")="org.elm.lang.core.stubs.StubImplementationsKt.factory"
    elementTypeFactory("ExposedUnionConstructor")="org.elm.lang.core.stubs.StubImplementationsKt.factory"
    elementTypeFactory("ExposedUnionConstructors")="org.elm.lang.core.stubs.StubImplementationsKt.factory"
    elementTypeFactory("ValueDeclaration")="org.elm.lang.core.stubs.StubImplementationsKt.factory"
    elementTypeFactory("PortAnnotation")="org.elm.lang.core.stubs.StubImplementationsKt.factory"

    // TODO [drop 0.18] remove this stub
    elementTypeFactory("OperatorDeclarationLeft")="org.elm.lang.core.stubs.StubImplementationsKt.factory"

    tokens = [
        // tokens used by manual/external parse rules
        DOT='DOT'

        // tokens used by the lexer but not visible to PsiBuilder parser
        RESERVED='RESERVED'
        LINE_COMMENT='LINE_COMMENT'
        BLOCK_COMMENT='BLOCK_COMMENT'
        NEWLINE='NEWLINE'
        TAB='TAB'
    ]
}

/*
OVERVIEW
========
This BNF grammar is interpreted by GrammarKit to produce a parser. Normally GrammarKit
will also generate the `PsiElement`s along with the grammar, but this leads to an explosion
of interfaces, implementation classes and convoluted inheritance. So instead we configure
GrammarKit to only generate 2 things:

- the parser (gen/org/elm/lang/core/parser/ElmParser.java)
- the element types (gen/org/elm/lang/core/psi/ElmTypes.java)

The actual PsiElement sub-classes which correspond to non-private grammar rules in this
BNF are created and maintained by hand. The upside of this is that you can freely modify
those PsiElement classes to implement additional interfaces, add helper methods and add
doc comments. The downside is that they must be kept in-sync with the parser (and thereby
this file).

Each non-private rule corresponds to a PsiElement subclass of the same name prefixed by
`Elm`. For instance, the `ModuleDeclaration` rule corresponds to the `ElmModuleDeclaration`
PsiElement subclass.

CODE FORMATTING
===============
- tokens are in ALL_CAPS
- grammar rules are in CamelCase
- meta rules and recoverWhile predicates should be in snake_case
 */

ElmFile ::= !<<eof>> Module
    {pin=1}


private Module ::=
    ModuleDeclaration? VIRTUAL_END_DECL? ImportList? VIRTUAL_END_DECL? TopDeclList?

ModuleDeclaration ::=
    PORT? MODULE UpperCaseQID ExposingList
    | 'effect' MODULE UpperCaseQID WHERE Record ExposingList
    { pin(".*")=UpperCaseQID; recoverWhile=module_recover }

// Recover from a module declaration by finding the next top-level statement. Typically
// it will be sufficient to just search for VIRTUAL_END_DECL, but in the case of an Elm
// file without a module declaration, we also need to look for the beginning of an import
// or a top-level declaration. For performance reasons, tokens should be checked before
// rules.
private module_recover ::= !(VIRTUAL_END_DECL | IMPORT | Declaration)


private generic_recover ::= !VIRTUAL_END_DECL

private ImportList ::= Import (VIRTUAL_END_DECL Import)*
private Import ::= ImportClause
    {pin=1 recoverWhile=generic_recover}


private TopDeclList ::= TopDecl (VIRTUAL_END_DECL TopDecl)*
private TopDecl ::= !<<eof>> Declaration
    {pin=1 recoverWhile=generic_recover}


// MODULE DECLARATION


ExposingList ::= EXPOSING LEFT_PARENTHESIS (DOUBLE_DOT | ExposedItem MoreExposedItems?) RIGHT_PARENTHESIS
    {pin=1}

private ExposedItem ::=
    ExposedValue
    | ExposedType
    | ExposedOperator
    {recoverWhile=exposed_item_recover}

private exposed_item_recover ::= !(COMMA | RIGHT_PARENTHESIS | VIRTUAL_END_DECL)

private MoreExposedItems ::= (COMMA ExposedItem)+ {pin(".*")=1}

ExposedValue ::= LOWER_CASE_IDENTIFIER

ExposedType ::=
    UPPER_CASE_IDENTIFIER [ExposedUnionConstructors]

ExposedUnionConstructors ::=
    LEFT_PARENTHESIS (DOUBLE_DOT | (<<non_empty_list ExposedUnionConstructor>>)) RIGHT_PARENTHESIS

ExposedUnionConstructor ::=
    UPPER_CASE_IDENTIFIER

ExposedOperator ::=
    OperatorAsFunction_inner

// WHITESPACE-SENSITIVE RULES


external DotWithoutWhitespace ::= parseDotWithoutWhitespace
external DotWithoutTrailingWhitespace ::= parseDotWithoutTrailingWhitespace
external DotWithoutLeadingWhitespace ::= parseDotWithoutLeadingWhitespace
external MinusWithoutTrailingWhitespace ::= parseMinusWithoutTrailingWhitespace

UpperCaseQID ::= UPPER_CASE_IDENTIFIER (DotWithoutWhitespace UPPER_CASE_IDENTIFIER)* !DotWithoutLeadingWhitespace
ValueQID ::= (UPPER_CASE_IDENTIFIER DotWithoutWhitespace)* LOWER_CASE_IDENTIFIER !DotWithoutLeadingWhitespace
FieldAccess ::= LOWER_CASE_IDENTIFIER (DotWithoutWhitespace LOWER_CASE_IDENTIFIER)+ !DotWithoutLeadingWhitespace
ExpressionAccessor ::= (DotWithoutWhitespace LOWER_CASE_IDENTIFIER)+ !DotWithoutLeadingWhitespace
FieldAccessorFunction ::= DotWithoutTrailingWhitespace LOWER_CASE_IDENTIFIER !DotWithoutLeadingWhitespace


// IMPORT DECLARATION


ImportClause ::= IMPORT UpperCaseQID [AsClause] [ExposingList]
    { pin = 2 }

AsClause ::= AS UPPER_CASE_IDENTIFIER



// TOP-LEVEL DECLARATIONS


private Declaration ::=
    ValueDeclaration
    | TypeAliasDeclaration
    | TypeDeclaration
    | TypeAnnotation
    | PortAnnotation
    | OperatorConfig
    | InfixDeclaration

ValueDeclaration ::= InternalValueDeclarationLeft EQ Expression
    { pin = 'EQ' }

private InternalValueDeclarationLeft ::=
    FunctionDeclarationLeft
    | OperatorDeclarationLeft
    | Pattern

FunctionDeclarationLeft ::=
    LOWER_CASE_IDENTIFIER FunctionDeclarationPattern*

 // TODO [drop 0.18] remove OperatorDeclarationLeft
OperatorDeclarationLeft ::=
    OperatorAsFunction_inner FunctionDeclarationPattern*

private FunctionDeclarationPattern ::=
        AnythingPattern
        | LowerPattern
        | TuplePattern
        | Unit
        | ListPattern // Always a partial pattern, but not a syntax error
        | RecordPattern
        | Literal // Always a partial pattern, but not a syntax error
        | ParenthesizedPattern

// TYPE DECLARATIONS AND REFERENCES


TypeDeclaration ::=
    TYPE UPPER_CASE_IDENTIFIER (LowerTypeName)* EQ UnionMember MoreUnionMembers?
    { pin = 2 }

LowerTypeName ::= LOWER_CASE_IDENTIFIER

UnionMember ::=
    UPPER_CASE_IDENTIFIER SingleTypeRef*
    { pin = 1; recoverWhile = union_member_recover }

private union_member_recover ::= !(PIPE|VIRTUAL_END_DECL)

private MoreUnionMembers ::= (PIPE UnionMember)+
    { pin(".*") = 1 }

TypeAliasDeclaration ::=
    TYPE ALIAS UPPER_CASE_IDENTIFIER LowerTypeName* EQ TypeRef
    { pin = 3 }

TypeRef ::=
    TypeRefInner (ARROW TypeRefInner)*

private TypeRefInner ::=
    ParametricTypeRef
    | SingleTypeRef

ParametricTypeRef ::= UpperCaseQID (SingleTypeRef)+

private SingleTypeRef ::=
    UpperPathTypeRef
    | TypeVariableRef
    | RecordType
    | TupleType
    | LEFT_PARENTHESIS TypeRef RIGHT_PARENTHESIS

UpperPathTypeRef ::=
    UpperCaseQID

TypeVariableRef ::=
    LOWER_CASE_IDENTIFIER

RecordType ::=
    LEFT_BRACE [[RecordBase] <<non_empty_list2 FieldType>>] RIGHT_BRACE
    { pin = 1 }

FieldType ::=
    LOWER_CASE_IDENTIFIER COLON TypeRef

TupleType ::=
    Unit
    | LEFT_PARENTHESIS TypeRef (COMMA TypeRef)+ RIGHT_PARENTHESIS


 // TODO [drop 0.18] remove the OperatorAsFunction_inner clause
TypeAnnotation ::=
    (LOWER_CASE_IDENTIFIER|OperatorAsFunction_inner) COLON TypeRef
    { pin = 'COLON' }

PortAnnotation ::= PORT LOWER_CASE_IDENTIFIER COLON TypeRef
    { pin = 'COLON' }



// EXPRESSIONS


Expression ::= CallOrOperand (Operator CallOrOperand)*

Operator ::=
    OPERATOR_IDENTIFIER

OperatorAsFunction ::=
    OperatorAsFunction_inner

private OperatorAsFunction_inner ::=
    LEFT_PARENTHESIS OPERATOR_IDENTIFIER RIGHT_PARENTHESIS

private CallOrOperand ::=
    FunctionCall
    | Operand

private Operand ::=
    Literal
    | RecordWithAccessor
    | ExpressionWithAccessor
    | NegateExpression
    | FieldAccess
    | ValueExpr
    | FieldAccessorFunction
    | OperatorAsFunction
    | ParenthesizedExpression
    | TupleConstructor
    | Tuple
    | List
    | Record
    | IfElse
    | CaseOf
    | LetIn
    | AnonymousFunction
    | GlslCode

// Various tokens that can begin a new operand. Useful for parse error recovery.
private expr_delimiters ::= COMMA | LEFT_BRACE | LEFT_PARENTHESIS | LEFT_SQUARE_BRACKET

ExpressionWithAccessor ::=
    ParenthesizedExpression ExpressionAccessor

RecordWithAccessor ::=
    Record ExpressionAccessor

NegateExpression ::= MinusWithoutTrailingWhitespace Expression
    { pin = 1 }

FunctionCall ::=
    FunctionCallTarget Operand+
    { pin = 2 }

private FunctionCallTarget ::=
    RecordWithAccessor
    | ExpressionWithAccessor
    | FieldAccess
    | ValueExpr
    | FieldAccessorFunction
    | OperatorAsFunction
    | ParenthesizedExpression
    | TupleConstructor
    | AnonymousFunction

private ParenthesizedExpression ::= LEFT_PARENTHESIS Expression RIGHT_PARENTHESIS

private Literal ::=
    CharConstant
    | NumberConstant
    | StringConstant

CharConstant ::= OPEN_CHAR StringPart CLOSE_CHAR
    { pin = 1 }
NumberConstant ::= NUMBER_LITERAL
StringConstant ::= OPEN_QUOTE StringParts CLOSE_QUOTE
    { pin = 1 }

private StringPart ::= REGULAR_STRING_PART | STRING_ESCAPE | INVALID_STRING_ESCAPE
private StringParts ::= StringPart*
    { recoverWhile = string_recover }

private string_recover ::= !(CLOSE_QUOTE|VIRTUAL_END_DECL|expr_delimiters)

AnonymousFunction ::= BACKSLASH Pattern+ ARROW Expression

ValueExpr ::=
    ValueQID
    | UpperCaseQID

private Tuple ::=
    Unit
    | NonEmptyTuple

NonEmptyTuple ::=
    LEFT_PARENTHESIS Expression (COMMA Expression)+ RIGHT_PARENTHESIS
    {pin(".*")=2}

// TODO [drop 0.18] remove this
TupleConstructor ::=
    LEFT_PARENTHESIS COMMA+ RIGHT_PARENTHESIS

Unit ::=
    LEFT_PARENTHESIS RIGHT_PARENTHESIS

List ::=
    LEFT_SQUARE_BRACKET [<<non_empty_list2 Expression>>] RIGHT_SQUARE_BRACKET
    { pin = 1 }

Record ::=
    LEFT_BRACE RecordInner? RIGHT_BRACE
    { pin = 1 }

RecordBaseIdentifier ::=
    LOWER_CASE_IDENTIFIER

private RecordBase ::=
    RecordBaseIdentifier PIPE

private RecordInner ::=
    RecordBase? RecordInnerFields
    { recoverWhile = record_inner_recover }

private RecordInnerFields ::=
    Field (COMMA Field)*
    { pin(".*") = 1 }

private record_inner_recover ::=
    !(RIGHT_BRACE|VIRTUAL_END_SECTION|VIRTUAL_END_DECL)

Field ::=
    LOWER_CASE_IDENTIFIER EQ Expression
    { pin = 2 }


IfElse ::=
    IF Expression THEN Expression (ELSE IF Expression THEN Expression)* ELSE Expression
    { pin = 'IF' }


CaseOf ::=
    CASE Expression CaseOfTail
    { pin = 'CASE' }

// This decomposition of rules is really lame, but it's the only way I could figure out
// how to get GrammarKit to not be too greedy and try parsing top-level declarations as
// if they were case branches. Is there a better way?
private CaseOfTail ::=
    OF CaseOfTail2
    {pin=1}

private CaseOfTail2 ::=
    VIRTUAL_OPEN_SECTION CaseOfBranch MoreCaseOfBranches? (VIRTUAL_END_SECTION|<<eof>>)
    {pin=1}

CaseOfBranch ::=
    Pattern ARROW Expression
    { pin = 1; recoverWhile=case_branch_recover }

private case_branch_recover ::= !(VIRTUAL_END_DECL|VIRTUAL_END_SECTION)

private MoreCaseOfBranches ::= (VIRTUAL_END_DECL CaseOfBranch)+
    { pin(".*")=1 }


LetIn ::=
    LET LetInTail
    { pin = 'LET' }

private LetInTail ::=
    VIRTUAL_OPEN_SECTION InnerDeclaration MoreInnerDeclarations? VIRTUAL_END_SECTION IN Expression
    { pin = 1 }

private InnerDeclaration ::=
    ValueDeclaration
    | TypeAnnotation
    { recoverWhile=inner_decl_recover }

private inner_decl_recover ::= !(VIRTUAL_END_DECL|VIRTUAL_END_SECTION)

private MoreInnerDeclarations ::= (VIRTUAL_END_DECL InnerDeclaration)+
    { pin(".*")=1 }


// PATTERNS


Pattern ::=
    (ConsPattern | SinglePattern) [PatternAs]

PatternAs ::= AS LOWER_CASE_IDENTIFIER

ConsPattern ::= SinglePattern ('::' SinglePattern)+

private SinglePattern ::=
    ParenthesizedPattern
    | AnythingPattern
    | LowerPattern
    | UnionPattern
    | TuplePattern
    | Unit
    | ListPattern
    | RecordPattern
    | Literal

LowerPattern ::= LOWER_CASE_IDENTIFIER

AnythingPattern ::= UNDERSCORE

RecordPattern ::=
    LEFT_BRACE <<non_empty_list LowerPattern>> RIGHT_BRACE

ListPattern ::=
    LEFT_SQUARE_BRACKET [<<non_empty_list Pattern>>] RIGHT_SQUARE_BRACKET

UnionPattern ::=
    UpperCaseQID UnionArgumentPattern*

private UnionArgumentPattern ::=
    AnythingPattern
    | LowerPattern
    | TuplePattern
    | UpperCaseQID
    | Unit
    | ListPattern
    | RecordPattern
    | Literal
    | ParenthesizedPattern

TuplePattern ::=
    LEFT_PARENTHESIS Pattern (COMMA Pattern)+ RIGHT_PARENTHESIS

private ParenthesizedPattern ::=
    LEFT_PARENTHESIS Pattern RIGHT_PARENTHESIS



// MISC


InfixDeclaration ::=
    INFIX ('left' | 'right' | 'non') NUMBER_LITERAL OperatorAsFunction_inner EQ ValueExpr
    { pin = 'EQ' }


GlslCode ::= START_GLSL_CODE GLSL_CODE_CONTENT* END_GLSL_CODE;


 // TODO [drop 0.18] remove OperatorConfig
OperatorConfig ::=
    OperatorConfigKeyword NUMBER_LITERAL OPERATOR_IDENTIFIER

 // TODO [drop 0.18] remove OperatorConfigKeyword
private OperatorConfigKeyword ::=
    INFIXL
    | INFIX
    | INFIXR




// UTILITY


private meta non_empty_list ::= <<p>> (COMMA <<p>>)*
// TODO [kl] consolidate once I make sure that this is safe to use everywhere
private meta non_empty_list2 ::= <<p>> (COMMA <<p>>)* {pin(".*")=1}
