plugins {
  id "org.jetbrains.kotlin.jvm" version "1.4.0"
  id "org.jetbrains.intellij" version "0.4.26"
  id "org.jetbrains.grammarkit" version "2021.1.3"
}

repositories {
//    mavenLocal()
    mavenCentral()
}

ext.kotlinVersion = '1.4.0'
ext.junitVersion = '4.12'
ext.jacksonVersion = '2.9.8'
ext.markdownVersion = '0.1.46'

// ---------------
// JAVA AND KOTLIN

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: "org.jetbrains.intellij"
apply plugin: "org.jetbrains.grammarkit"

// JDK compatibility
sourceCompatibility = javaVersion
targetCompatibility = javaTargetVersion
tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

// Kotlin compatibility
tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "11"
    }
}

def genRoot = file('gen')

sourceSets {
    main {
        kotlin.srcDir 'src/main/kotlin'
        java.srcDir genRoot
        resources.srcDir 'src/main/resources'
    }

    test {
        kotlin.srcDir 'src/test/kotlin'
        resources.srcDir 'src/test/resources'
    }
}

// ---------------------------------------------
// INTELLIJ (GRADLE STUFF FOR PLUGIN DEVELOPERS)

apply plugin: 'org.jetbrains.intellij'

intellij {
    version intellijPlatformVersion
    pluginName 'intellij-elm'
    updateSinceUntilBuild false
}

// ------------------------------------------------
// IDEA (GRADLE STUFF FOR NORMAL INTELLIJ PROJECTS)

apply plugin: 'idea'

idea {
    project {
        jdkName = javaVersion
        languageLevel = javaVersion
    }
    module {
        generatedSourceDirs += genRoot
    }
}

// -----------
// GRAMMAR KIT

apply plugin: 'org.jetbrains.grammarkit'
import org.jetbrains.grammarkit.tasks.GenerateLexer
import org.jetbrains.grammarkit.tasks.GenerateParser

task generateElmLexer(type: GenerateLexer) {
    source = "src/main/grammars/ElmLexer.flex"
    targetDir = "gen/org/elm/lang/core/lexer/"
    targetClass = "_ElmLexer"
    purgeOldFiles = true
    skeleton = "src/main/grammars/lexer.skeleton"
}

task generateElmParser(type: GenerateParser) {
    source = "src/main/grammars/ElmParser.bnf"
    targetRoot = genRoot
    pathToParser = '/org/elm/lang/core/parser/ElmParser.java'
    pathToPsiRoot = '/org/elm/lang/core/psi'
    purgeOldFiles = true
}

task generateCode {
    dependsOn generateElmParser
    dependsOn generateElmLexer
}

// ----------------
// MY PLUGIN ITSELF

group 'org.elm'
version myPluginVersion

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    compileOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile 'com.github.ajalt:colormath:1.4.1'
    compile("org.jetbrains:markdown:$markdownVersion") {
        exclude module: "kotlin-runtime"
        exclude module: "kotlin-stdlib"
    }
    testCompile "junit:junit:$junitVersion"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
}

compileKotlin {
    dependsOn generateCode
}
